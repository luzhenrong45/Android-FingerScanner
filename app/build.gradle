
apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "mobi.thinkchange.android.fingerscannercn"
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 108
        versionName "6.6"

        // Default channel
        manifestPlaceholders = [TC_CHANNEL_VALUE: "_4466001001"]
    }

    signingConfigs {
        debug {
            // No debug config for the moment
        }

        release {
            // You can configure keystore file and its password here
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            // Specify the output apk name
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "FingerScanner_v${defaultConfig.versionName}${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    // Multiple channels
    productFlavors {
        _4466001001 {}
        _4466001002 {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [TC_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.+'
    compile 'com.mcxiaoke.volley:library:1.0.+'
    compile 'de.greenrobot:eventbus:2.4.0'
    //    compile 'com.afollestad:material-dialogs:0.7.3.0'
    compile files('libs/easyandroidanimationslibrary-v0.5.jar')
    compile project(':libTCCustomTheme')
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile files('libs/BaiduLBS_Android.jar')
    compile files('libs/gson-2.2.4.jar')
    compile(name: 'titanic-library-release', ext: 'aar')
    compile project(':Shimmer-android')
    compile project(':material-dialogs')
}
